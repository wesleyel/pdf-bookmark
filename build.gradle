plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
    id 'org.beryx.jlink' version '2.12.0'
}
group 'com.ifnoelse'
version '1.0.7'

mainClassName = "com.ifnoelse.pdf.gui.Main"

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

sourceCompatibility = 11

repositories {
    maven {
        url "https://maven.aliyun.com/nexus/content/groups/public/"
    }
    mavenCentral()
}

javafx {
    version = "11"
    modules = [ 'javafx.controls' ]
}

jlink {
    launcher {
        name = 'pdf-bookmark'
    }
    jlinkZip {
        group = 'distribution'
    }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.10.2'
    implementation group: 'com.itextpdf', name: 'itextpdf', version: '5.5.13'
//    implementation group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.46'
}

tasks.register('manualTest', JavaExec) {
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'test.ManualTest'
}

// Add this to handle the testCompile configuration issue
configurations {
    testCompile {
        extendsFrom testImplementation
    }
}
